cmake_minimum_required(VERSION 3.10)

option(CAITLYN_DEV "Development mode" ON)
option(CAITLYN_TEST "Build tests" OFF)
option(CAITLYN_INSTALL "Install library" OFF)

if (CAITLYN_INSTALL)
  set(CAITLYN_DEV OFF)
endif()

if (CAITLYN_DEV)
  set(CAITLYN_TEST ON)
endif()

set(CAITLYN_MAJOR_VERSION 1)
set(CAITLYN_MINOR_VERSION 0)
set(CAITLYN_PATCH_VERSION 0)

set(
  CAITLYN_VERSION_STRING
  "${CAITLYN_MAJOR_VERSION}.${CAITLYN_MINOR_VERSION}.${CAITLYN_PATCH_VERSION}"
)

project(caitlyn VERSION ${CAITLYN_VERSION_STRING} LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 11)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

set(CAITLYN_INCLUDE_DIR "${CMAKE_SOURCE_DIR}/include")

if(CAITLYN_DEV)
  if(CMAKE_CXX_COMPILER_ID STREQUAL "MSVC")
    set(CMAKE_CXX_FLAGS "/std:c++11 /EHsc /Wall /W3 /permissive")
  else()
    list(APPEND CMAKE_CXX_FLAGS "-Wall -Wextra -pedantic")
  endif()
endif()

if(CAITLYN_TEST)
  enable_testing()
  add_subdirectory(test)
endif()

if(CAITLYN_INSTALL)
  set(CMAKE_INSTALL_PREFIX ${CMAKE_SOURCE_DIR})

  set(CAITLYN_TARGET_NAME ${PROJECT_NAME})
  set(CAITLYN_TARGET_NAMESPACE "${PROJECT_NAME}::")
  set(CAITLYN_INSTALL_PATH "lib/cmake/caitlyn")

  set(CAITLYN_EXPORT_NAME "${PROJECT_NAME}Targets")

  set(CAITLYN_CMAKE_ROOT_CONFIG_NAME "${PROJECT_NAME}Config.cmake.in")
  set(CAITLYN_CMAKE_CONFIG_NAME "${PROJECT_NAME}Config.cmake")
  set(CAITLYN_CMAKE_CONFIG_VERSION_NAME "${PROJECT_NAME}ConfigVersion.cmake")
  set(CAITLYN_CMAKE_TARGETS_NAME "${PROJECT_NAME}Targets.cmake")

  add_library(${CAITLYN_TARGET_NAME} INTERFACE)

  target_include_directories(
    ${CAITLYN_TARGET_NAME} INTERFACE
    $<BUILD_INTERFACE:${CAITLYN_INCLUDE_DIR}>
    $<INSTALL_INTERFACE:include>
  )

  include(CMakePackageConfigHelpers)
  write_basic_package_version_file(
    "${CMAKE_CURRENT_BINARY_DIR}/${CAITLYN_CMAKE_CONFIG_VERSION_NAME}"
    VERSION ${CAITLYN_VERSION_STRING}
    COMPATIBILITY AnyNewerVersion
  )

  configure_package_config_file(
    "${CMAKE_CURRENT_SOURCE_DIR}/cmake/${CAITLYN_CMAKE_ROOT_CONFIG_NAME}"
    "${CMAKE_CURRENT_BINARY_DIR}/${CAITLYN_CMAKE_CONFIG_NAME}"
    INSTALL_DESTINATION ${CAITLYN_INSTALL_PATH}
  )

  install(
    TARGETS ${CAITLYN_TARGET_NAME}
    EXPORT ${CAITLYN_EXPORT_NAME}
    INCLUDES DESTINATION include
  )

  install(
    EXPORT ${CAITLYN_EXPORT_NAME}
    FILE ${CAITLYN_CMAKE_TARGETS_NAME}
    NAMESPACE ${CAITLYN_TARGET_NAMESPACE}
    DESTINATION ${CAITLYN_INSTALL_PATH}
  )

  install(
    FILES "${CMAKE_CURRENT_BINARY_DIR}/${CAITLYN_CMAKE_CONFIG_NAME}"
          "${CMAKE_CURRENT_BINARY_DIR}/${CAITLYN_CMAKE_CONFIG_VERSION_NAME}"
    DESTINATION ${CAITLYN_INSTALL_PATH}
  )

  export(
    EXPORT ${CAITLYN_EXPORT_NAME}
    FILE "${CMAKE_CURRENT_BINARY_DIR}/${CAITLYN_CMAKE_TARGETS_NAME}"
    NAMESPACE ${CAITLYN_TARGET_NAMESPACE}
  )
endif()
